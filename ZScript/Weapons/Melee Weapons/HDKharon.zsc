// ------------------------------------------------------------
// Kharon
// ------------------------------------------------------------

class HDKharonPuff:HDBulletPuff{
	default{
		stamina 5;scale 1;
		hdbulletpuff.scarechance 5;
	}
	override void postbeginplay(){
		super.postbeginplay();
		if(max(abs(pos.x),abs(pos.y),abs(pos.z))>=32768){destroy();return;}
		if(!random(0,scarechance)){
			actor fff=spawn("idledummy",pos,ALLOW_REPLACE);
			fff.stamina=random(60,120);
			hdmobai.frighten(fff,256);
		}
		// A_StartSound("misc/bullethit",0,0,0.5*stamina*stamina);
		A_StartSound("weapons/kharonwall",CHAN_7,0,1.02*stamina*stamina);
		A_ChangeVelocity(-0.4,0,frandom(0.1,0.4),CVF_RELATIVE);
		trymove(pos.xy+vel.xy,false);
		int stm=stamina;
		// this doesn't actually do anything
		// fadeafter=frandom(0,1);
		scale*=frandom(0.9,1.1);
		for(int i=0;i<stamina;i++){
			A_SpawnParticle("white",
				SPF_RELATIVE,70,frandom(4,20)*getdefaultbytype((class<actor>)(missilename)).scale.x,0,
				frandom(-3,3),frandom(-3,3),frandom(0,4),
				frandom(-0.4,0.4)*stm,frandom(-0.4,0.4)*stm,frandom(0.4,1.2)*stm,
				frandom(-0.1,0.1),frandom(-0.1,0.1),-1.
			);

			// actor ch=spawn(missilename,self.pos,ALLOW_REPLACE);
			// ch.vel=self.vel+(random(-stm,stm),random(-stm,stm),random(-2,12));
		}
	}
	states{
	spawn:
		TNT1 A 1;
		stop;
	}
}

class HDKharonSpark : Actor {
	Default {
		+NOCLIP
		+ROLLSPRITE
		+ROLLCENTER
		+FORCEXYBILLBOARD
		+NOGRAVITY
		+ALLOWPARTICLES
		+RANDOMIZE
		+ZDOOMTRANS
		Mass 1;
		Radius 1;
		Height 1;
		scale 0.9;
	}
	States {
		Spawn:
			TNT1 A 1 A_StartSound("misc/bullethit");
			KRP0 ABC 1 Bright;
		Melee:
			TNT1 A 1;
			Stop;
	}
}

class HDKharonSparkL : HDKharonSpark {
	States {
		Spawn:
			TNT1 A 1 nodelay A_SetRoll(roll + 7);
			KRP0 ABC 1 Bright;
			stop;
	}
}

class HDKharonSparkR : HDKharonSpark {
	States {
		Spawn:
			TNT1 A 1 nodelay A_SetRoll(roll - 7);
			KRP0 ABC 1 Bright;
			stop;
	}
}

class HDKharonSparkV : HDKharonSpark {
	States {
		Spawn:
			TNT1 A 1 nodelay A_SetRoll(roll + 60);
			KRP0 ABC 1 Bright;
			stop;
	}
}

class HDKharon : HDCoreBaseMeleeWeapon {

	default {
		scale 0.75;

		obituary "$OB_KHARON";
		weapon.selectionorder 200;
		weapon.kickback 15;
		weapon.bobrangex 0.2;
		weapon.slotpriority 0.75;
		tag "$TAG_KHARON";
		HDWeapon.refid HDLD_KHARON;
		
		meleeRange                          75.0;
		HDCoreBaseMeleeWeapon.swingDmgMult  2.0;
		HDCoreBaseMeleeWeapon.corpseDmgMult 1.0;
		HDCoreBaseMeleeWeapon.attackFlags   HDCMW_DO_HEADSHOT|HDCMW_DO_PUFF|HDCMW_RECOIL_ATTACKEE;
	}

	override double weaponbulk() { return 85; }

	override double gunmass() { return 11; }
	
	override string,double getpickupsprite() { return "KRDRA0", 1.0; }

	// TODO: Localize
	override string getHelpText() {
		LocalizeHelp();
		return
		WEPHELP_FIRE.."  Light Swing\n"
		..WEPHELP_ALTFIRE.."   Heavy Swing\n"
		..WEPHELP_RELOAD.."   Sheath/Withdraw Attack\n"
		..WEPHELP_UNLOAD.."   Throw\n"
		//..WEPHELP_ZOOM.."+"..WEPHELP_DROP.."   Drop misc. items\n"
		;
	}

	override double getWeaponAttackHeight() {
		return owner.height - 12;
	}

	override name getWeaponDamageType() {
		return _swingMode == HDCMW_ATTACK_THRUST ? 'piercing' : 'slashing';
	}

	override double getWeaponHeadShotDamageBonus() {
		return frandom(1.8, 2.1);
	}

	override int getWeaponHeadShotStun(int dmg) {
		return 0;
	}

	override bool getWeaponLeftHanded() {
		return false;
	}

	override string getWeaponPuff() {
		switch (_swingMode) {
			case HDCMW_ATTACK_THRUST:
			case HDCMW_ATTACK_UP:
			case HDCMW_ATTACK_DOWN:
				return "HDKharonSparkV";
			case HDCMW_ATTACK_LEFT:
				return "HDKharonSparkL";
			case HDCMW_ATTACK_RIGHT:
				return "HDKharonSparkR";
			default:
				return "HDKharonSpark";
		}
	}

	override void doWeaponPuff(name puffType, int aOff, double dist, int pOff) {
		super.doWeaponPuff(puffType, aOff, dist, pOff);
		// super.doWeaponPuff("WallChunker", aOff, dist, pOff);
		// super.doWeaponPuff(zerk ? "HDSmokeChunk" : "BulletPuffSmall", aOff, dist, pOff);
		super.doWeaponPuff("HDKharonPuff", aOff, dist, pOff);
	}

	override void doWeaponAttack(Actor attackee, int dmg, name dmgType) {
		super.doWeaponAttack(attackee, dmg, dmgType);

		HDBleedingWound.inflict(attackee, int(dmg * frandom(0.85, 1.5)), 8);
	}

	override void doHeadShot(Actor attackee, int dmg) {
		HDCore.log('HDCore.BaseMeleeWeapon', LOGGING_DEBUG, "HEAD SHOT");
	}

	override void doAttackSound(Actor attackee) {
		if (attackee.bNOBLOOD) {
			attackee.A_StartSound("weapons/kharonwall", CHAN_BODY, 0, 1.25);
		} else {
			attackee.A_StartSound("weapons/kharoncut", CHAN_BODY, 0, 0.5);
		}
	}

	override double calculateAttackDamage(double dmg, FLineTraceData atkLine, double deltaAngle, double deltaPitch, int flags) {

		// If Distraction Punch (kick + punch?)
		// multiply damage by 150%
		// Otherwise add bonus based on facing/distance
		if (flicked) {
			dmg *= 1.5;
		} else {

			// TODO: Extract & Hoist into HDCoreLib?
			dmg += 2.0
				* (atkLine.hitActor
					? HDMath.TowardsEachOther(owner, atkLine.hitActor)
					: (atkLine.hitLocation - owner.pos).length() - (atkLine.hitLocation - (owner.pos + owner.vel)).length()
				);
		}

		dmg += owner.player.onGround ? min(getWeaponSwingDamageBonus(deltaAngle, deltaPitch) * 5, dmg * 3) : 0.0;

		// // TODO: Extract & Hoist into HDCoreLib?
		// if (owner.player.onGround) {
		// 	dmg += min(getWeaponSwingDamageBonus(deltaAngle, deltaPitch) * 100, dmg * 2);
		// } else {
		// 	dmg *= 0.5;
		// }

		// Multiply damage based on player strength,
		// whether attack resulted in a headshot,
		// and whether the actor hit is dead
		// TODO: Extract & Hoist into HDCoreLib?
		dmg *= (strength * frandom(0.95, 1.5))
			* (atkLine.hitActor && shouldDoHeadShot(atkLine, flags) ? getWeaponHeadShotDamageBonus() : 1.0)
			* (atkLine.hitActor && HDMath.isDead(atkLine.hitActor)) ? getWeaponCorpseDamageBonus() : 1.0;

		return dmg;
	}
	
	states {

		select0:
			TNT1 A 3 {
				invoker.weaponstatus[KHARON_SHEATHED] = 1;
			}
			KRN4 A 0 A_JumpIf(pressingFire(), "swingfromsheath");
			KRN4 V 0 A_JumpIf(invoker.weaponstatus[KHARON_SHEATHED], "select0small");
			KRN5 A 0;
			goto select0small;

		deselect0:
		sheath_deselect:
			KRN4 P 0 A_JumpIf(invoker.weaponstatus[KHARON_SHEATHED], "deselect_s");
			KRN4 PONMLKJIHGFEDCBA 2;
			// KRN4 QRSTUV 2;
			TNT1 A 0 {
				invoker.weaponstatus[KHARON_SHEATHED] = 1;
			}
		deselectfull:
			KRN4 A 0;
			goto deselect0small;
			deselect_s:
			KRN4 V 0;
			goto deselect0small;

		ready:
			TNT1 A 0 A_JumpIf(!invoker.weaponstatus[KHARON_SHEATHED], "fullready");
			goto readysheathed;

		unsheath_fr:
			KRN4 UTSRQ 2;
		unsheath:
			TNT1 A 0 {
				invoker.weaponstatus[KHARON_SHEATHED] = 0;
			}
			KRN4 ABCDEGIJKLMNOP 2;
		fullready:
			KRNS A 0 A_JumpIf(invoker.wasHolding && pressingFire(), "nope");
			KRN5 A 1 {
				A_WeaponReady(WRF_ALL);
				invoker.flicked = invoker.wasHolding = false;
			}
			goto readyend;
		readysheathed:
			KRN4 V 1 A_JumpIf(invoker.wasHolding && pressingFire(), "nope");
			KRN4 V 1 {
				A_WeaponReady(WRF_ALL|WRF_NOSECONDARY);
				invoker.flicked = invoker.wasHolding = false;
			}
			goto readyend;
		reload:
			TNT1 A 0 A_JumpIf(invoker.weaponstatus[KHARON_SHEATHED], "unsheath_fr");
			KRN4 PONMLKJIHGFEDCBA 2;
			---- A 0 {
				invoker.weaponstatus[KHARON_SHEATHED] = 1;
			}
			KRN4 QRSTU 2;
			goto readyend;
		fire:
			KRN5 A 0 A_JumpIf(invoker.weaponstatus[KHARON_SHEATHED], "sheathstrike");
			KRN5 BCD 2;
		swing1:
			KRN3 A 0 A_StartSound("weapons/kharonswing",CHAN_7);
			KRN1 A 1;
			KRN1 B 1 A_MeleeWeaponAttack(HDCMW_ATTACK_RIGHT, dmg: 15, aOff: -12, pOff: -2, puffType: 'HDKharonSparkL');
			KRN1 C 1 {
				A_MeleeWeaponAttack(HDCMW_ATTACK_RIGHT, dmg: 30, puffType: 'HDKharonSparkL');
				// if (HDPlayerPawn(self)) HDPlayerPawn(self).fatigue += 1;
			}
			KRN1 D 1 A_MeleeWeaponAttack(HDCMW_ATTACK_RIGHT, dmg: 15, aOff: 12, pOff: 2, puffType: 'HDKharonSparkL');
			KRN1 EF 1;
			TNT1 A 7;
			#### A 1 A_JumpIf(pressingFire(), "swing2");
			goto recoverswing;
		swing2:
			KRN3 A 0 A_StartSound("weapons/kharonswing", CHAN_7);
			KRN2 A 1;
			KRN2 B 1 A_MeleeWeaponAttack(HDCMW_ATTACK_LEFT, dmg: 15, aOff: 12, pOff: -2, puffType: 'HDKharonSparkR');
			KRN2 C 1 {
				A_MeleeWeaponAttack(HDCMW_ATTACK_LEFT, dmg: 30, puffType: 'HDKharonSparkR');

				if (HDPlayerPawn(self)) HDPlayerPawn(self).fatigue += 1;
			}
			KRN2 D 1 A_MeleeWeaponAttack(HDCMW_ATTACK_LEFT, dmg: 15, aOff: -12, pOff: 2, puffType: 'HDKharonSparkR');
			KRN2 EF 1;
			TNT1 A 7;
			#### A 1 A_JumpIf(pressingFire(), "swing1");
			goto recoverswing;
		sheathstrike:
			KRN4 WX 2;
			KRN7 ABCD 2;
		SSHold:
			KRN7 D 1; // A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
			#### A 0 A_JumpIf(pressingReload(),"SSCancel");
			#### A 0 A_JumpIf(pressingFire(),"SSHold");
			TNT1 A 0 A_ReFire("SSHold");
			goto swingfromsheath;
		SSCancel:
			KRN7 CBA 2;
			KRN4 XWVVVV 2;
			goto readyend;
		swingfromsheath:
			KRN7 EF 2;
			KRN3 A 0 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
			TNT1 A 0 {
				invoker.weaponstatus[KHARON_SHEATHED] = 0;
			}
			KRN3 A 0 A_StartSound("weapons/kharonswing", CHAN_7);
			KRN6 AB 1;
			KRN6 C 1 A_MeleeWeaponAttack(HDCMW_ATTACK_UP, dmg: 25, aOff: 7, pOff: 10);
			KRN6 D 1 {
				A_MeleeWeaponAttack(HDCMW_ATTACK_UP, dmg: 45);

				if (HDPlayerPawn(self)) HDPlayerPawn(self).fatigue += 3;
			}
			KRN6 E 1 A_MeleeWeaponAttack(HDCMW_ATTACK_UP, dmg: 25, aOff: -7, pOff: -10);
			KRN6 E 1 A_MeleeWeaponAttack(HDCMW_ATTACK_UP, dmg: 25, aOff: -7, pOff: -10);
			KRN6 F 1;
			TNT1 A 17;
			#### A 0 A_JumpIf(pressingFire(), "swing1");
			goto recoverswing;
		altfire:
		althold:
			TNT1 A 0;
			KRN5 BCD 2;
		startfire:
			#### A 0 A_JumpIf(invoker.zerk, "zerkswingstart");
			KRN3 A 0 A_StartSound("weapons/pocket", CHAN_6);
			KRN3 FEDCBA 2;
			KRN3 C 0 A_WeaponBusy();
			goto holdswing;
		zerkswingstart:
			KRN3 A 0 A_StartSound("weapons/pocket", CHAN_6);
			KRN3 FEDCBA 1;
			KRN3 C 0 A_WeaponBusy();
			goto holdswing;
		holdswing:
			KRN3 A 1; // A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
			#### A 0 A_JumpIf(pressingReload(), "SwingWithdraw");
			#### A 0 A_JumpIf(pressingFire(), "thrustattack");
			TNT1 A 0 A_ReFire("holdswing");
			goto swinghard;
		thrustattack:
			KRN8 AC 1;
			KRN8 E 1 A_MeleeWeaponAttack(HDCMW_ATTACK_THRUST, dmg: 15, pOff: 2);
			KRN8 D 2;
			KRN8 CBA 3;
			goto holdswing;
		swingreturn:
			TNT1 A 3;
			TNT1 A 2 A_StartSound("weapons/pocket", CHAN_6);
			goto holdswing;
		swinghard:
			#### A 0 A_JumpIf(invoker.zerk, "zerkswinghard");
			KRN3 BDF 1;
			TNT1 A 3;
			#### A 0 A_Overlay(5, "hitcheck", false);
			KRN3 A 0 {
				A_StartSound("weapons/kharonswing", CHAN_7);

				if (HDPlayerPawn(self)) HDPlayerPawn(self).fatigue += 3;
			}
			KRN3 GH 1;
			KRN3 IJKL 1;
			// #### A 0 A_JumpIf(pressingAltFire(),"altfire");
			TNT1 AAAAAA 1 A_JumpIf(pressingFire(), "swing2");
			#### A 0 A_JumpIf(pressingFire(), "swing2");
			#### A 0 A_JumpIf(pressingAltFire(), "startfire");
			TNT1 A 0 A_ReFire("startfire");
			TNT1 A 0 A_WeaponBusy();
			goto recoverswing;

		recoverswing:
			TNT1 A 2;
			KRN5 DCB 2;
			#### A 0 A_JumpIf(pressingFire(), "nope");
			goto readyend;
		
		SwingWithdraw:
			KRN3 ABCDEF 2;
			KRN5 A 2;
			goto nope;
			
		zerkswinghard:
			KRN3 BDF 1;
			TNT1 A 1;
			#### A 0 A_Overlay(5, "hitcheckzerk", false);
			KRN3 A 0 {
				A_StartSound("weapons/kharonswing", CHAN_7);

				if (HDPlayerPawn(self)) HDPlayerPawn(self).fatigue += 3;
			}
			KRN3 GH 1;
			KRN3 IJKL 1;
			// #### A 0 A_JumpIf(pressingAltFire(),"altfire");
			TNT1 A 3;
			#### A 0 A_JumpIf(pressingFire(), "swingreturn");
			#### A 0 A_JumpIf(pressingAltFire(), "startfire");
			TNT1 A 0 A_ReFire("startfire");
			TNT1 A 0 A_WeaponBusy();
			goto recoverswing;

		firemode:
			goto nope;
		
		unload:
		YEET:
			---- A 1 {
				if (player && HDWeapon(player.readyweapon)) {
					player.cmd.buttons |= BT_ZOOM;
					DropInventory(player.readyweapon);
				}
			}
			TNT1 A 0;
			goto nope;
			
		hitcheck:
			TNT1 A 1 A_MeleeWeaponAttack(HDCMW_ATTACK_LEFT, dmg: 20, aOff: -20);
			TNT1 A 1 {
				A_MeleeWeaponAttack(HDCMW_ATTACK_LEFT, dmg: 25, aOff: -10);
				A_MeleeWeaponAttack(HDCMW_ATTACK_LEFT, dmg: 30);
				A_MeleeWeaponAttack(HDCMW_ATTACK_LEFT, dmg: 25, aOff: 10);
			}
			TNT1 A 1 A_MeleeWeaponAttack(HDCMW_ATTACK_LEFT, dmg: 20, aOff: 20);
		stop;
		
		hitcheckzerk:
			TNT1 AAA 1 A_MeleeWeaponAttack(HDCMW_ATTACK_LEFT, dmg: invoker.flicked ? 340 : 300);
		stop;
		
		// IGOTEM:
		// 	TNT1 A 1;
		// 	stop;
			
		spawn:
			KRDR A -1;
			stop;
	}
}

enum kharonstatus {
	KHARON_JUSTUNLOAD = 1,
 
	KHARON_STATUS     = 0,
	KHARON_SHEATHED   = 1,

	// imitation version of the one from demonsteele
	// KHARON_REAL       = 2
};
