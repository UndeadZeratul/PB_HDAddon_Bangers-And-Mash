// ------------------------------------------------------------
// Fireaxe
// ------------------------------------------------------------
class HDAxePuff : HDBulletPuff {
	default {
		stamina 5;
		scale 0.6;
		
		HDBulletPuff.scareChance 5;
	}

	override void postBeginPlay() {
		super.postBeginPlay();

		if (max(abs(pos.x), abs(pos.y), abs(pos.z)) >= 32768) {
			destroy();
			return;
		}

		if (!random(0, scarechance)) {
			actor fff = spawn("idledummy", pos, ALLOW_REPLACE);
			fff.stamina = random(60, 120);
			HDMobAI.frighten(fff, 256);
		}

		A_StartSound("misc/bullethit", 0, 0, 0.5 * stamina * stamina);
		A_StartSound("weapons/axewall", CHAN_7, 0, 1.02 * stamina * stamina);
		A_ChangeVelocity(-0.4, 0, frandom(0.1, 0.4), CVF_RELATIVE);
		tryMove(pos.xy + vel.xy, false);

		int stm = stamina;
		// this doesn't actually do anything
		// fadeafter=frandom(0, 1);
		scale *= frandom(0.9, 1.1);
		for (int i = 0; i < stamina; i++) {
			A_SpawnParticle("gray",
				SPF_RELATIVE, 70, frandom(4, 20) * getdefaultbytype((Class<Actor>)(missilename)).scale.x, 0,
				frandom(-3, 3), frandom(-3, 3), frandom(0, 4),
				frandom(-0.4, 0.4) * stm, frandom(-0.4, 0.4) * stm, frandom(0.4, 1.2) * stm,
				frandom(-0.1, 0.1), frandom(-0.1, 0.1), -1.0
			);

			// actor ch=spawn(missilename, self.pos, ALLOW_REPLACE);
			// ch.vel=self.vel+(random(-stm, stm), random(-stm, stm), random(-2, 12));
		}
	}

	states {
		spawn:
			TNT1 A 1 nodelay {
				if (tracer) {
					// if(tracer.bcorpse)hdf.give(tracer, "SawGib", random(5, 10));
					HDBleedingWound.inflict(tracer, 10, 3);
				}
			}
			stop;

		crash:
			TNT1 A 1;
			stop;
	}
}

class HDFireAxe : HDCoreBaseMeleeWeapon {
	default {
		scale 0.75;

		obituary "$OB_FIREAXE";
		weapon.selectionorder 200;
		weapon.kickback 15;
		weapon.bobrangex 0.2;
		weapon.slotpriority 0.75;
		inventory.pickupmessage "$PICKUP_FIREAXE";
		tag "$TAG_FIREAXE";
		HDWeapon.refid HDLD_FAXE;
		
		damageType                          'slashing';
		meleeRange                          70.0;
		HDCoreBaseMeleeWeapon.swingDmgMult  2.0;
		HDCoreBaseMeleeWeapon.corpseDmgMult 1.5;
		HDCoreBaseMeleeWeapon.attackFlags   HDCMW_DO_HEADSHOT|HDCMW_DO_PUFF|HDCMW_DO_WALLBUST|HDCMW_DO_WINDOWBUST|HDCMW_RECOIL_ATTACKEE;
	}

	override double weaponbulk() { return 165; }

	override double gunmass() { return 11; }
	
	override string,double getpickupsprite() { return "FAXPA0", 1.0; }
	
	// TODO: Localize
	override string getHelpText() {
		LocalizeHelp();
		return
		WEPHELP_FIRE.."  Swing\n"
		..WEPHELP_ALTFIRE.."   Lunge\n"
		..WEPHELP_RELOAD.."   Kick then Swing/Withdraw Attack\n"
		..WEPHELP_UNLOAD.."   Throw\n"
		//..WEPHELP_ZOOM.."+"..WEPHELP_DROP.."   Drop misc. items\n"
		;
	}
	
	// TODO: Refactor & Hoist into HDCoreLib?
	static void kick(actor kicker, actor kickee, actor kicking) {
		kickee.A_StartSound("weapons/smack", CHAN_BODY);

		bool kzk = HDZerk.isZerk(kicker);
		kickee.damagemobj(kicking, kicker, kzk ? random(20, 40) : random(10, 20), "bashing");
		
		if (kickee && !kickee.bNOPAIN && kickee.health > 0 && random(0, 4)) HDMobBase.forcePain(kickee);
		
		Vector3 kickdir = (kickee.pos - kicker.pos).unit();

		if (kickee) kickee.vel = kickdir * (kzk ? 10 : 2);
		kicker.vel -= kickdir;
	}

	override double getWeaponAttackHeight() {
		return owner.height - 12;
	}

	override double getWeaponHeadShotDamageBonus() {
		return frandom(2.1, 2.3);
	}

	override int getWeaponHeadShotStun(int dmg) {
		return 0;
	}

	override bool getWeaponLeftHanded() {
		return false;
	}

	override string getWeaponPuff() {
		return "HDAxePuff";
	}

	override double getWeaponWallBustWidth(double dmg) {
		return dmg * 0.1;
	}

	override double getWeaponWallBustDepth(double dmg) {
		return dmg * 0.05;
	}

	override void doWeaponPuff(name puffType, int aOff, double dist, int pOff) {
		super.doWeaponPuff(puffType, aOff, dist, pOff);
		super.doWeaponPuff("WallChunker", aOff, dist, pOff);
		super.doWeaponPuff(zerk ? "HDSmokeChunk" : "BulletPuffSmall", aOff, dist, pOff);
	}

	override void doWeaponAttack(Actor attackee, int dmg, name dmgType) {
		super.doWeaponAttack(attackee, dmg, dmgType);

		HDBleedingWound.inflict(attackee, dmg, frandom(10, 12));
	}

	override void doHeadShot(Actor attackee, int dmg) {
		HDCore.log('HDCore.BaseMeleeWeapon', LOGGING_DEBUG, "HEAD SHOT");
	}

	override void doAttackSound(Actor attackee) {
		if (attackee.bNOBLOOD) {
			attackee.A_StartSound("weapons/axewall", CHAN_BODY, 0, 1.25);
		} else {
			attackee.A_StartSound("weapons/axeflesh", CHAN_BODY, 0, 0.5);
			attackee.A_StartSound("weapons/axesplat", CHAN_6, 0, 1);
		}
	}

	override double calculateAttackDamage(double dmg, FLineTraceData atkLine, double deltaAngle, double deltaPitch, int flags) {

		// If Distraction Punch (kick + punch?)
		// multiply damage by 150%
		// Otherwise add bonus based on facing/distance
		if (flicked) {
			dmg *= 1.5;
		} else {

			// TODO: Extract & Hoist into HDCoreLib?
			dmg += 2.0 * (atkLine.hitActor
				? HDMath.TowardsEachOther(owner, atkLine.hitActor)
				: (atkLine.hitLocation - owner.pos).length() - (atkLine.hitLocation - (owner.pos + owner.vel)).length()
			);
		}

        dmg += owner.player.onGround ? min(getWeaponSwingDamageBonus(deltaAngle, deltaPitch) * 5, dmg * 3) : 0.0;
		
		// // TODO: Extract & Hoist into HDCoreLib?
		// if (owner.player.onGround) {
		// 	dmg += min(getWeaponSwingDamageBonus(deltaAngle, deltaPitch) * 100, dmg * 2);
		// } else {
		// 	dmg *= 0.5;
		// }

		// Multiply damage based on player strength,
		// whether attack resulted in a headshot,
		// and whether the actor hit is dead
		// TODO: Extract & Hoist into HDCoreLib?
		dmg *= (strength * frandom(0.95, 1.5))
			* (atkLine.hitActor && shouldDoHeadShot(atkLine, flags) ? getWeaponHeadShotDamageBonus() : 1.0)
			* (atkLine.hitActor && HDMath.isDead(atkLine.hitActor)) ? getWeaponCorpseDamageBonus() : 1.0;

		return dmg;
	}
	
	states {

		select0:
			FAX0 A 0;
			goto select0big;

		deselect0:
			FAX0 A 0;
			goto deselect0big;

		ready:
			FAX0 A 0 A_JumpIf(invoker.wasHolding && pressingFire(), "nope");
			FAX0 A 1 {
				A_WeaponReady(WRF_ALL);
				invoker.flicked = invoker.wasHolding = false;
			}
			goto readyend;

		reload:
			TNT1 A 0 A_Overlay(-999999, "kick", false);
			FAX1 ABCD 3;
			FAX1 E 2 HDPunch(20);
			FAX1 FGHIJ 2;
			FAX1 KKLLMMM 2;
			goto readyend;

		fire:
		hold:
		althold:
			TNT1 A 0;
		startfire:
			#### A 0 A_JumpIf(invoker.zerk, "zerkswingstart");
			FAX0 A 0 A_StartSound("weapons/pocket", CHAN_6);
			FAX0 A 0;
			FAX0 BC 3;
			FAX0 C 0 A_WeaponBusy();
			FAX0 C 2 offset(25, 64);
			FAX0 C 2 offset(40, 86);
			TNT1 A 4 offset(0, 32);
			goto holdswing;
		zerkswingstart:
			FAX0 A 0 A_StartSound("weapons/pocket", CHAN_6);
			FAX0 A 0;
			FAX0 BC 1;
			FAX0 C 0 A_WeaponBusy();
			FAX0 C 1 offset(25, 64);
			FAX0 C 1 offset(40, 86);
			TNT1 A 2 offset(1, 32);
			goto holdswing;
		holdswing:
			TNT1 A 1; // A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
			#### A 0 A_JumpIf(pressingReload(), "recoverswing");
			#### A 0 A_JumpIf(pressingFire(), "holdswing");
			TNT1 A 0 A_ReFire("holdswing");
			goto swing;

		swingreturn:
			TNT1 A 7;
			TNT1 A 5 A_StartSound("weapons/pocket", CHAN_6);
			goto holdswing;

		swing:
			#### A 0 A_JumpIf(invoker.zerk, "zerkswing");
			FAX0 A 0 A_StartSound("weapons/axeswing", CHAN_7);
			FAX0 DE 1;
			#### D 0 A_Overlay(5, "hitcheck", false);
			#### F 1 {
				if (HDPlayerPawn(self)) HDPlayerPawn(self).fatigue += 4;
			}
			#### GH 1;
			// #### A 0 A_JumpIf(pressingAltFire(),"altfire");
			TNT1 A 6;
			#### A 0 A_JumpIf(pressingFire(), "swingreturn");
			#### A 0 A_JumpIf(pressingAltFire(), "lungealt");
			TNT1 A 0 A_ReFire("holdswing");
			FAX0 A 0 A_WeaponBusy();
			goto recoverswing;

		recoverswing:
			FAX0 A 2 offset(10, 84);
			FAX0 A 2 offset(9, 72);
			FAX0 A 2 offset(8, 60);
			FAX0 A 1 offset(7, 52);
			FAX0 A 1 offset(5, 42);
			FAX0 A 1 offset(3, 38);
			FAX0 A 1 offset(3, 35);
			#### A 0 A_JumpIf(pressingFire(), "nope");
			goto readyend;
			
		zerkswing:
			FAX0 A 0 A_StartSound("weapons/axeswing", CHAN_7);
			FAX0 DE 1;
			#### D 0 A_Overlay(5, "hitcheckzerk", false);
			#### FGH 1;
			// #### A 0 A_JumpIf(pressingAltFire(),"altfire");
			TNT1 A 3;
			#### A 0 A_JumpIf(pressingFire(), "swingreturn");
			#### A 0 A_JumpIf(pressingAltFire(), "lungealt");
			TNT1 A 0 A_ReFire("holdswing");
			FAX0 A 0 A_WeaponBusy();
			goto recoverswing;

		altfire:
			FAX0 A 0 A_StartSound("weapons/pocket", CHAN_6);
			FAX0 B 3;
			FAX0 C 0 {
				A_WeaponBusy();
				A_Overlay(-999999, "lunge", false);
			}
			FAX0 C 3;
			FAX0 C 2 offset(25, 64);
			FAX0 C 2 offset(40, 86);
			TNT1 A 6 offset(0, 32);
			#### A 0 A_JumpIf(pressingFire(), "holdswing");
			#### A 0 A_JumpIf(pressingAltFire(), "kickattack");
			goto swing;

		lunge:
			TNT1 A 2 A_CheckFloor("lunge2");
			stop;

		lunge2:
			TNT1 A 0 A_Lunge(3, fatigueIncr: 3, abortState: 'recoverswing');
			TNT1 AA 1 {
				if (invoker.zerk) A_Recoil(-random(12, 24));
			}
			TNT1 A 1 A_Recoil(-4);
			stop;

		lungealt:
			TNT1 A 3 {
				A_WeaponBusy();
				A_Overlay(-999999, "lunge", false);
			}
			TNT1 A 13;
			#### A 0 A_JumpIf(pressingFire(), "holdswing");
			#### A 0 A_JumpIf(pressingAltFire(), "kickattack");
			goto swing;
		KickAttack:
			TNT1 A 0 A_Overlay(-999999, "kick", false);
			goto recoverswing;
		kick:
			TNT1 A 13 {
				if (HDPlayerPawn(self)) HDPlayerPawn(self).fatigue += 2;
				FLineTraceData ktl;
				LineTrace(angle, radius * 1.6, 0, offsetz: 10, data: ktl);
				if (ktl.hitactor) invoker.kick(self, ktl. hitactor, invoker);
				if (countinv("HDZerk")) A_SetTics(8);
			}
			stop;

		firemode:
			goto nope;
		
		unload:
		YEET:
			---- A 1 {
				if (player && HDWeapon(player.readyweapon)) {
					player.cmd.buttons |= BT_ZOOM;
					DropInventory(player.readyweapon);
				}
			}
			TNT1 A 0;
			goto nope;

		hittarget:
			FAX0 IJKL 1;
			TNT1 A 5;
			#### A 0 A_JumpIf(pressingFire(), "swingreturn");
			#### A 0 A_JumpIf(pressingAltFire(), "lungealt");
			TNT1 A 0 A_ReFire("holdswing");
			FAX0 A 0 A_WeaponBusy();
			goto recoverswing;
			
		hitcheck:
			TNT1 A 1 A_MeleeWeaponAttack(HDCMW_ATTACK_DOWN, dmg: 75);
			stop;
		
		hitcheckzerk:
			TNT1 A 1 A_MeleeWeaponAttack(HDCMW_ATTACK_DOWN, dmg: invoker.flicked ? 140 : 100);
			stop;

		spawn:
			FAXP A -1;
			stop;
	}
}
