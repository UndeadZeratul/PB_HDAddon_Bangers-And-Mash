// ------------------------------------------------------------
// Sledgehammer
// ------------------------------------------------------------
class HDSledgePuff : HDBulletPuff {
	default {
		stamina 5;
		scale 0.6;

		HDBulletPuff.scareChance 5;
	}

	override void postBeginPlay() {
		super.postBeginPlay();

		if (max(abs(pos.x), abs(pos.y), abs(pos.z)) >= 32768) {
			destroy();
			return;
		}

		if (!random(0, scarechance)) {
			actor fff = spawn("idledummy", pos, ALLOW_REPLACE);
			fff.stamina = random(60, 120);
			HDMobAI.frighten(fff, 256);
		}

		A_StartSound("misc/bullethit", 0, 0, 0.5 * stamina * stamina);
		A_StartSound("weapons/sledgewall", CHAN_7, 0, 1.02 * stamina * stamina);
		A_ChangeVelocity(-0.4, 0, frandom(0.1, 0.4), CVF_RELATIVE);
		tryMove(pos.xy + vel.xy, false);

		int stm = stamina;
		// this doesn't actually do anything
		// fadeafter=frandom(0, 1);
		scale *= frandom(0.9, 1.1);
		for (int i = 0; i < stamina; i++) {
			A_SpawnParticle("gray",
				SPF_RELATIVE, 70, frandom(4, 20) * getdefaultbytype((Class<Actor>)(missilename)).scale.x, 0,
				frandom(-3, 3), frandom(-3, 3), frandom(0, 4),
				frandom(-0.4, 0.4) * stm, frandom(-0.4, 0.4) * stm, frandom(0.4, 1.2) * stm,
				frandom(-0.1, 0.1), frandom(-0.1, 0.1), -1.0
			);

			// actor ch=spawn(missilename, self.pos, ALLOW_REPLACE);
			// ch.vel=self.vel+(random(-stm, stm), random(-stm, stm), random(-2, 12));
		}
	}

	states {
		spawn:
			TNT1 A 1 nodelay {
				if (tracer) {
					// if(tracer.bcorpse)hdf.give(tracer, "SawGib", random(5, 10));
					HDBleedingWound.inflict(tracer, 10);
				}
			}
			stop;

		crash:
			TNT1 A 1;
			stop;
	}
}
	
class HDSledgehammer : HDCoreBaseMeleeWeapon {
	default {
		scale 0.75;

		obituary "$OB_SLEDGEHAMMER";
		weapon.selectionorder 200;
		weapon.kickback 15;
		weapon.bobrangex 0.2;
		weapon.slotpriority 0.75;
		inventory.pickupmessage "$PICKUP_SLEDGEHAMMER";
		tag "$TAG_SLEDGEHAMMER";
		HDWeapon.refid HDLD_SLEDGEHAMMER;

        damageType                         'bashing';
        meleeRange                         70.0;
        HDCoreBaseMeleeWeapon.swingDmgMult 2.0;
        HDCoreBaseMeleeWeapon.attackFlags  HDCMW_DO_HEADSHOT|HDCMW_DO_PUFF|HDCMW_DO_WALLBUST|HDCMW_DO_WINDOWBUST|HDCMW_RECOIL_ATTACKEE;
	}
	
	override double weaponbulk() { return 185; }

	override double gunmass() { return 20; }

	override string,double getpickupsprite() { return "SLH0Z0", 1.0; }
	
	// TODO: Localize
	override string getHelpText() {
		LocalizeHelp();
		return
		WEPHELP_FIRE.."  Swing\n"
		..WEPHELP_ALTFIRE.."   Lunge\n"
		..WEPHELP_RELOAD.."   Kick then Swing/Withdraw Attack\n"
		..WEPHELP_UNLOAD.."   Throw\n"
		//..WEPHELP_ZOOM.."+"..WEPHELP_DROP.."   Drop misc. items\n"
		;
	}

	// TODO: Refactor & Hoist into HDCoreLib?
	static void kick(actor kicker, actor kickee, actor kicking) {
		kickee.A_StartSound("weapons/smack", CHAN_BODY);

		bool kzk = HDZerk.isZerk(kicker);
		kickee.damagemobj(kicking, kicker, kzk ? random(20, 40) : random(10, 20), "bashing");

		if (kickee && !kickee.bNOPAIN && kickee.health > 0 && random(0, 4)) HDMobBase.forcePain(kickee);

		Vector3 kickdir = (kickee.pos - kicker.pos).unit();

		if (kickee) kickee.vel = kickdir * (kzk ? 10 : 2);
		kicker.vel -= kickdir;
	}
	
	override double getWeaponAttackHeight() {
		return owner.height - 12;
	}

	override double getWeaponHeadShotDamageBonus() {
		return frandom(3.9, 4.1);
	}

	override int getWeaponHeadShotStun(int dmg) {
		return 0;
	}

	override bool getWeaponLeftHanded() {
		return false;
	}

	override string getWeaponPuff() {
		return "HDSledgePuff";
	}

	override double getWeaponWallBustWidth(double dmg) {
		return dmg * 0.5;
	}

	override double getWeaponWallBustDepth(double dmg) {
		return dmg * 0.1;
	}

	override void doWeaponPuff(name puffType, int aOff, double dist, int pOff) {
		super.doWeaponPuff(puffType, aOff, dist, pOff);
		super.doWeaponPuff("WallChunker", aOff, dist, pOff);
		super.doWeaponPuff(zerk ? "HDSmokeChunk" : "BulletPuffSmall", aOff, dist, pOff);
	}

	override void doWeaponAttack(Actor attackee, int dmg, name dmgType) {
		super.doWeaponAttack(attackee, dmg, dmgType);
		
		// setWeaponState("IGOTEM", 5);

		HDBleedingWound.inflict(attackee, dmg, frandom(5, 8));
	}

	override void doHeadShot(Actor attackee, int dmg) {
		HDCore.log('HDCore.BaseMeleeWeapon', LOGGING_DEBUG, "HEAD SHOT");
	}

	override void doAttackSound(Actor attackee) {
		if (attackee.bNOBLOOD) {
			attackee.A_StartSound("weapons/sledgewall", CHAN_BODY, 0, 1.25);
		} else {
			attackee.A_StartSound("weapons/sledgeflesh", CHAN_BODY, 0, 0.5);
		}
	}

	override double calculateAttackDamage(double dmg, FLineTraceData atkLine, double deltaAngle, double deltaPitch, int flags) {

		// If Distraction Punch (kick + punch?)
		// multiply damage by 150%
		// Otherwise add bonus based on facing/distance
		if (flicked) {
			dmg *= 1.5;
		} else {

			// TODO: Extract & Hoist into HDCoreLib?
			dmg += 2.0 * (atkLine.hitActor
				? HDMath.TowardsEachOther(owner, atkLine.hitActor)
				: (atkLine.hitLocation - owner.pos).length() - (atkLine.hitLocation - (owner.pos + owner.vel)).length()
			);
		}

		dmg += owner.player.onGround ? min(getWeaponSwingDamageBonus(deltaAngle, deltaPitch) * 5, dmg * 3) : 0.0;

		// // TODO: Extract & Hoist into HDCoreLib?
		// if (owner.player.onGround) {
		// 	dmg += min(getWeaponSwingDamageBonus(deltaAngle, deltaPitch) * 100, dmg * 2);
		// } else {
		// 	dmg *= 0.5;
		// }

		// Multiply damage based on player strength,
		// whether attack resulted in a headshot,
		// and whether the actor hit is dead
		// TODO: Extract & Hoist into HDCoreLib?
		dmg *= (strength * frandom(0.85, 1.5))
			* (atkLine.hitActor && shouldDoHeadShot(atkLine, flags) ? getWeaponHeadShotDamageBonus() : 1.0)
			* (atkLine.hitActor && HDMath.isDead(atkLine.hitActor)) ? getWeaponCorpseDamageBonus() : 1.0;

		return dmg;
	}
	
	states {

		droop:
			TNT1 A 1 {
				if (pitch < frandom(5, 8) && !gunbraced()) {
					if (countinv("IsMoving") > 2 && invoker.zerk) {
						A_MuzzleClimb(
							frandom(-0.2, 0.2),
							frandom(0.1, clamp(1 - pitch, 0.1, 0.3))
						);
					} else {
						A_MuzzleClimb(
							frandom(-0.06, 0.06),
							frandom(0.1, clamp(1 - pitch, 0.06, 0.12))
						);
					}
				}
			}
			loop;
			
		dropper:
			TNT1 A 1 {
				A_MuzzleClimb(frandom(-0.01, 0.01), frandom(0.1, clamp(1 - pitch, 1.55, 1.6)));
			}
			stop;

		dropper2:
			TNT1 A 1 {
				A_MuzzleClimb(frandom(-0.01, 0.01), frandom(0.1, clamp(1 - pitch, 0.55, 0.6)));
			}
			stop;
			
		unf:
			SLH0 NN 4 A_WeaponOffset(random(-2, 2), random(30, 34), WOF_INTERPOLATE);
			TNT1 A 0 {
				// TODO: confirm if needed?  Seems to check if it should compare?
				if (HDPlayerPawn(self)) HDPlayerPawn(self).barehanded == 1;
			}
			loop;

		select0:
			SLH0 A 0 A_Overlay(2, "droop");
			goto select0bfg;

		deselect0:
			SLH0 A 0 A_ClearOverlays(2, 3);
			goto deselect0bfg;

		ready:
			SLH0 A 0 A_ClearOverlays(3, 3);
			SLH0 A 0 {
				//length manipulation
				invoker.barrellength = 12.5;
				invoker.barrelwidth = 5.25;
				invoker.barreldepth = 1.5;
			}
			SLH0 A 0 A_JumpIf(invoker.wasHolding && pressingFire(), "nope");
			SLH0 A 1 {
				A_WeaponReady(WRF_ALL);
				invoker.flicked = invoker.wasHolding = false;
			}
			goto readyend;

		reload:
			TNT1 A 0 A_Overlay(-999999, "kick", false);
		fire:
		althold:
			TNT1 A 0;
		startfire:
			#### A 0 A_JumpIf(invoker.zerk, "zerkswingstart");
			SLH0 A 0 A_StartSound("weapons/pocket", CHAN_6);
			// SLH0 A 0 A_WeaponBusy();
			SLH0 B 3;
			SLH0 C 3 {
				invoker.barrellength = 5.5;
				invoker.barrelwidth = 0;
				invoker.barreldepth = 0;
			}
			SLH0 D 0 A_WeaponBusy();
			SLH0 D 2 offset(25, 64);
			SLH0 D 2 offset(40, 86);
			TNT1 A 4 offset(-10, 32);
		bruh1:
			#### A 0 A_JumpIf(invoker.zerk, "bruh2");
			SLH0 EFGHIJKLMN 2 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
			SLH0 N 0 A_Overlay(3, "unf");
			goto holdswing;
		zerkswingstart:
			SLH0 A 0 A_StartSound("weapons/pocket", CHAN_6);
			// SLH0 A 0 A_WeaponBusy();
			SLH0 B 1;
			SLH0 C 1 {
				invoker.barrellength = 5.5;
				invoker.barrelwidth = 0;
				invoker.barreldepth = 0;
			}
			SLH0 D 1 A_WeaponBusy();
			SLH0 D 1 offset(25, 64);
			SLH0 D 1 offset(40, 86);
			TNT1 A 2 offset(-10, 32);
		bruh2:
			SLH0 EGIKM 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
			SLH0 N 0 A_Overlay(3, "unf");
			goto holdswing;
		holdswing:
			TNT1 A 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH|WRF_ALLOWUSER4);
			#### A 0 A_JumpIf(pressingReload(), "recoverswing");
			#### A 0 A_JumpIf(pressingFire(), "holdswing");
			TNT1 A 0 A_ReFire("holdswing");
			---- A 0 A_ClearOverlays(3, 4);
			goto swing;
		HoldSwingAnim:
			TNT1 A 4 A_WeaponOffset(frandom(-2, 2), frandom(30, 34));
			loop;

		swingreturn:
			TNT1 A 7;
			TNT1 A 5 A_StartSound("weapons/pocket", CHAN_6);
			#### A 0 A_JumpIf(invoker.zerk, "bruh2");
			SLH0 EFGHIJKLMN 2;
			SLH0 N 0 A_Overlay(3, "unf");
		goto holdswing;

		swing:
			#### A 0 A_JumpIf(invoker.zerk, "zerkswing");
			#### A 0 {
				if (HDPlayerPawn(self)) HDPlayerPawn(self).fatigue += 5;
			}
			SLH1 A 0 A_StartSound("weapons/sledgeswing", CHAN_7);
			SLH1 ABCD 1 A_Overlay(15, "dropper", false);
			#### E 0 A_Overlay(5, "hitcheck", false);
			#### EFGHI 1 A_Overlay(15, "dropper", false);
			TNT1 A 6 A_Overlay(15, "dropper2", false);
			#### A 0 A_JumpIf(pressingFire(), "swingreturn");
			#### A 0 A_JumpIf(pressingAltFire(), "lungealt");
			// TNT1 A 0 A_ReFire("holdswing");
			SLH0 A 0 A_WeaponBusy();
			goto recoverswingcont;

		recoverswing:
			---- A 0 A_ClearOverlays(3, 3);
			SLH0 MLKJIHGFE 1 A_WeaponReady(WRF_NOFIRE|WRF_NOSWITCH);
			TNT1 A 5;
		recoverswingcont:
			SLH0 A 2 offset(10, 84);
			SLH0 A 2 offset(9, 72);
			SLH0 A 2 offset(8, 60);
			SLH0 A 1 offset(7, 52);
			SLH0 A 1 offset(5, 42);
			SLH0 A 1 offset(3, 38);
			SLH0 A 1 offset(3, 35);
			#### A 0 A_JumpIf(pressingFire(), "nope");
			goto readyend;
			
		zerkswing:
			SLH0 A 0 A_StartSound("weapons/sledgeswing", CHAN_7);
			SLH1 CD 1;
			#### D 0 A_Overlay(5, "hitcheckzerk", false);
			#### EFGHI 1;
			// #### A 0 A_JumpIf(pressingAltFire(), "altfire");
			TNT1 A 3;
			#### A 0 A_JumpIf(pressingFire(), "swingreturn");
			#### A 0 A_JumpIf(pressingAltFire(), "lungealt");
			TNT1 A 0 A_ReFire("holdswing");
			SLH0 A 0 A_WeaponBusy();
			goto recoverswingcont;
		altfire:
			SLH0 A 0 A_StartSound("weapons/pocket", CHAN_6);
			SLH0 B 3;
			SLH0 C 0 {
				A_WeaponBusy();
				A_Overlay(-999999, "lunge", false);
			}
			SLH0 C 3 {
				invoker.barrellength = 5.5;
				invoker.barrelwidth = 0;
				invoker.barreldepth = 0;
			}
			SLH0 C 2 offset(25, 64);
			SLH0 C 2 offset(40, 86);
			TNT1 A 6 offset(-10, 32);
			#### A 0 A_JumpIf(pressingAltFire(), "kickattack");
			#### A 0 {
				if (HDPlayerPawn(self)) HDPlayerPawn(self).fatigue += 10;
			}
			// SLH0 EGIKM 1;
			goto bruh1;

		lunge:
			TNT1 A 2 A_CheckFloor("lunge2");
			stop;

		lunge2:
			TNT1 A 0 A_Lunge(3, fatigueIncr: 3, abortState: 'recoverswing');
			TNT1 AA 1 {
				if (invoker.zerk) A_Recoil(-random(12, 24));
			}
			TNT1 A 1 A_Recoil(-4);
			stop;

		lungealt:
			TNT1 A 3 {
			A_WeaponBusy();
			A_Overlay(-999999, "lunge", false);
			}
			TNT1 A 13 {
				invoker.barrellength = 5.5;
				invoker.barrelwidth = 0;
				invoker.barreldepth = 0;
			}
			#### A 0 A_JumpIf(pressingFire(), "holdswing");
			#### A 0 A_JumpIf(pressingAltFire(), "kickattack");
			goto swing;
		KickAttack:
			TNT1 A 0 A_Overlay(-999999, "kick", false);
			goto recoverswingcont;
		kick:
			TNT1 A 13 {
				if (HDPlayerPawn(self)) HDPlayerPawn(self).fatigue += 2;
				FLineTraceData ktl;
				LineTrace(angle, radius * 1.6, 0, offsetz: 10, data: ktl);
				if (ktl.hitactor) invoker.kick(self, ktl.hitactor, invoker);
				if (invoker.zerk) A_SetTics(8);
			}
			stop;

		firemode:
			goto nope;
		
		unload:
		YEET:
			---- A 1 {
				if (player && HDWeapon(player.readyweapon)) {
					player.cmd.buttons |= BT_ZOOM;
					DropInventory(player.readyweapon);
				}
			}
			TNT1 A 0;
			goto nope;

		whoops:
			SLH2 A 2;
			SLH1 GHI 1;
			TNT1 A 5;
			#### A 0 A_JumpIf(pressingFire(), "swingreturn");
			#### A 0 A_JumpIf(pressingAltFire(), "lungealt");
			TNT1 A 0 A_ReFire("holdswing");
			SLH0 A 0 A_WeaponBusy();
			goto recoverswingcont;
			
		hitcheck:
			// TNT1 AA 1 A_MeleeWeaponAttack(HDCMW_ATTACK_DOWN, dmg: 125);
			TNT1 A 1 A_MeleeWeaponAttack(HDCMW_ATTACK_DOWN, dmg: 110);
			// TNT1 A 1 A_MeleeWeaponAttack(HDCMW_ATTACK_DOWN, dmg: 70);
		stop;
		
		hitcheckzerk:
			TNT1 A 1 A_MeleeWeaponAttack(HDCMW_ATTACK_DOWN, dmg: invoker.flicked ? 240 : 200);
		stop;
		
		// IGOTEM:
		// 	TNT1 A 1;
		// 	stop;
			
		spawn:
			SLH0 Z -1;
			stop;
	}
}
