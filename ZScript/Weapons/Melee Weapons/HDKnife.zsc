// ------------------------------------------------------------
// Combat Knife
// ------------------------------------------------------------
// class HDStabber : IdleDummy {
// 	default {
// 		+bloodlessimpact +nodecal +hittracer +puffonactors
// 		stamina 1;
// 	}
// }

class HDCombatKnife : HDCoreBaseMeleeWeapon {

	default {
		+HDWeapon.REVERSEGUNINERTIA
		+NODAMAGETHRUST

		radius 3;
		height 3;

		inventory.pickupmessage "$PICKUP_KNIFE";
		hdweapon.barrelsize 1,1,1;
		obituary "$OB_KNIFE";
		attacksound "*fist";
		weapon.kickback 15;
		weapon.slotpriority 2.1;
		scale 0.6;
		tag "$TAG_KNIFE";
		hdweapon.refid HDLD_KNIFE;
		
		damageType                         'slashing';
		meleeRange                         54.0;
		HDCoreBaseMeleeWeapon.swingDmgMult 2.0;
		HDCoreBaseMeleeWeapon.attackFlags  HDCMW_DO_HEADSHOT|HDCMW_DO_PUFF|HDCMW_RECOIL_ATTACKEE;
	}
	
	override double weaponbulk() { return 15; }

	override double gunmass() { return 3; }

	override string,double getpickupsprite() { return "KNFPZ0", 0.35; }

	// TODO: Localize
	override string getHelpText() {
		LocalizeHelp();
		return
		WEPHELP_FIRE.."  Stab\n"
		..WEPHELP_ALTFIRE.."   Lunge\n"
		..WEPHELP_RELOAD.."   Distracting strike\n"
		..WEPHELP_FIREMODE.."   Prepare throw\n"
		..WEPHELP_RELOAD.."+"..WEPHELP_FIRE.."   Rapid Stabs\n"
		//..WEPHELP_UNLOAD.."   Distracting projectile\n"
		;
	}

	override double getWeaponAttackHeight() {
		return owner.height - 12;
	}

	override double getWeaponHeadShotDamageBonus() {
		return frandom(2.1, 2.3);
	}

	override int getWeaponHeadShotStun(int dmg) {
		return 0;
	}

	override bool getWeaponLeftHanded() {
		return false;
	}

	override string getWeaponPuff() {
		return zerk ? "BulletPuffMedium" : "BulletPuffSmall";
	}

	override void doWeaponAttack(Actor attackee, int dmg, name dmgType) {
		super.doWeaponAttack(attackee, dmg, dmgType);

		HDBleedingWound.inflict(attackee, dmg, frandom(5, 8));
	}

	override void doHeadShot(Actor attackee, int dmg) {
		super.doHeadShot(attackee, dmg);

		HDBleedingWound.inflict(attackee, 5, frandom(5, 8), true);
	}

	override void doAttackSound(Actor attackee) {
		if(attackee.bNOBLOOD) {
			attackee.A_StartSound("weapons/kharonwall", CHAN_7, CHANF_OVERLAP, volume: 0.5, pitch: 1.1);
		} else {
			attackee.A_StartSound("weapons/kharoncut", CHAN_7, CHANF_OVERLAP, volume: 0.5, pitch: 1.1);
		}
	}

	override double calculateAttackDamage(double dmg, FLineTraceData atkLine, double deltaAngle, double deltaPitch, int flags) {

		// If Distraction Punch (kick + punch?)
		// multiply damage by 150%
		// Otherwise add bonus based on facing/distance
		if (flicked) {
			dmg *= 1.5;
		} else {

			// TODO: Extract & Hoist into HDCoreLib?
			dmg += 2.0 * (atkLine.hitActor
				? HDMath.TowardsEachOther(owner, atkLine.hitActor)
				: (atkLine.hitLocation - owner.pos).length() - (atkLine.hitLocation - (owner.pos + owner.vel)).length()
			);
		}

		dmg += owner.player.onGround ? min(getWeaponSwingDamageBonus(deltaAngle, deltaPitch) * 5, dmg * 3) : 0.0;

		// // TODO: Extract & Hoist into HDCoreLib?
		// if (owner.player.onGround) {
		// 	dmg += min(getWeaponSwingDamageBonus(deltaAngle, deltaPitch) * 100, dmg * 2);
		// } else {
		// 	dmg *= 0.5;
		// }

		// Multiply damage based on player strength,
		// whether attack resulted in a headshot,
		// and whether the actor hit is dead
		// TODO: Extract & Hoist into HDCoreLib?
		dmg *= (strength * frandom(1.1, 1.6))
			* (atkLine.hitActor && shouldDoHeadShot(atkLine, flags) ? getWeaponHeadShotDamageBonus() : 1.0)
			* (atkLine.hitActor && HDMath.isDead(atkLine.hitActor)) ? getWeaponCorpseDamageBonus() : 1.0;

		return dmg;
	}

	states {

		select:
			TNT1 A 0 {
				// these two don't actually work???
				A_OverlayFlags(PSP_WEAPON, PSPF_CVARFAST|PSPF_POWDOUBLE, false);
				A_OverlayFlags(PSP_FLASH, PSPF_CVARFAST|PSPF_POWDOUBLE, false);

				A_WeaponBusy();
				A_SetCrosshair(21);
				A_SetHelpText();
				
				invoker.weaponstatus[FRAGS_FORCE] = 0;
			}
			goto select0;
		select0:
			CKF0 E 0;
			goto select0small;
		deselect:
			TNT1 A 0 A_StartDeselect();
		deselect0:
			CKF0 E 0;
			goto deselect0small;
			
		ready:
			CKF0 E 0 A_JumpIf(invoker.wasHolding && pressingFire(), "nope");
			CKF0 E 1 {
				A_WeaponReady(WRF_ALL);
				invoker.flicked = invoker.wasHolding = false;
			}
			goto readyend;
		reload:
		flick:
			CKF0 A 1 offset(0, 50) A_Lunge(min(HDPlayerPawn(self).overloaded * 0.6, 4.0) - 4.0, fatigueIncr: 3, abortState: 'hold');
			#### A 1 offset(0, 36);
			#### A 0 A_JumpIf(invoker.zerk, "ZerkFlick");
			#### AAAAAAA 0 A_CustomPunch(1, 1, CPF_PULLIN, "HDFistPuncher", 36);
			goto flickend;
		zerkflick:
			#### AAAAAAA 0 A_CustomPunch(random(1, 3), 1, CPF_PULLIN, "HDFistPuncher", 36);
		flickend:
			#### AA 1 offset(0, 38) {
				invoker.flicked = true;
			}
			#### A 1 offset(0, 42);
			#### A 1 offset(0, 50);
			#### A 0 A_JumpIf(pressingFire(), "superstab");
			goto fire;
		fire:
		hold:
		althold:
		startfire:
			CKF0 A 0 A_JumpIf(invoker.zerk, "zerkpunch");
			goto punch;
		punch:
			CKF0 C 1 offset(0, 32) A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
			#### D 3 offset(6, 28) A_MeleeWeaponAttack(HDCMW_ATTACK_THRUST, dmg: 12);
			#### D 3 offset(0, 32) A_WeaponOffset(frandom(-3, 3), frandom(30, 34));
			#### CB 3;
			#### E 3;
			#### E 0 A_JumpIf(pressingAltFire(), "altfire");
			#### E 1 A_ReFire();
			goto ready;
		zerkpunch:
			CKF0 D 0 {
				A_Recoil(-1);
				A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
			}
			#### D 0 A_MeleeWeaponAttack(HDCMW_ATTACK_THRUST, dmg: invoker.flicked ? 40 : 30);
			#### D 3 A_WeaponOffset(frandom(-12, 12), frandom(30, 34));
			#### CBE 1;
			#### E 1;
			#### E 0 A_JumpIf(pressingAltFire(), "altfire");
			#### E 2 A_ReFire();
			goto ready;
		altfire:
			CKF0 E 1 offset(0, 36);
			#### E 1 offset(0, 50);
			#### E 2 A_CheckFloor("lunge");
			goto nope;
		lunge:
			TNT1 A 0 A_Lunge(min(HDPlayerPawn(self).overloaded * 0.6, 4.0) - 4.0, fatigueIncr: 3, abortState: 'hold');
			TNT1 AA 1 {
				if (invoker.zerk) A_Recoil(-random(12, 24));
			}
			TNT1 A 1 A_Recoil(-4);
		goto startfire;
		grabhold:
			CKF0 A 1 {
				invoker.weaponstatus[FRAGS_FORCE] = 50;
			}
			TNT1 A 0 A_JumpIf(pressingFire(), "YEET");
			TNT1 A 0 A_JumpIf(pressingFiremode(), "grabhold");
			goto nope;
		firemode:
		grab:
		grab2:
			CKF0 A 1 offset(0, 52);
			#### A 1 offset(0, 32);
			goto grabhold;
		YEET:
			---- A 1 {
				if (player && HDWeapon(player.readyweapon)) {
					player.cmd.buttons |= BT_ZOOM;
					DropInventory(player.readyweapon);
				}
			}
			TNT1 A 0;
			goto nope;
		spawn:
			KNFP X 0 A_JumpIf(invoker.bNOEXTREMEDEATH, "spawn2");
			KNFP Y 1;
			loop;
		spawn2:
			KNFP X 1 A_SetPitch(15 + pitch);
			loop;
		spawn3:
			KNFP X 1;
			// {
			// 	setorigin((pos + tracer.pos) * 0.5, true);
			// }
			loop;
		
		superstab:
			CKF0 A 0 A_JumpIf(invoker.zerk, "zerksuperstab");
			CKF0 C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
			#### D 0 {
				A_MeleeWeaponAttack(HDCMW_ATTACK_THRUST, dmg: 6);
				hdplayerpawn(self).fatigue += 1;
			}
			#### D 2 offset(-3, 34); // A_WeaponOffset(frandom(-3, 3), frandom(30, 34));
			#### CB 1;
			#### E 3;
			#### E 1 A_ReFire("superstab2");
			goto fire;
		superstab2:
			CKF0 C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
			#### D 0 {
				A_MeleeWeaponAttack(HDCMW_ATTACK_THRUST, dmg: 6);
				hdplayerpawn(self).fatigue += 1;
			}
			#### D 2 offset(6, 30); // A_WeaponOffset(frandom(-6, 6), frandom(30, 34));
			#### CB 1;
			#### E 3;
			#### E 1 A_ReFire("superstab3");
			goto fire;
		superstab3:
			CKF0 C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
			#### D 0 {
				A_MeleeWeaponAttack(HDCMW_ATTACK_THRUST, dmg: 6);
				hdplayerpawn(self).fatigue += 2;
			}
			#### D 3 offset(4, 32); // A_WeaponOffset(frandom(-8, 8), frandom(30, 34));
			#### CB 1;
			#### E 3;
			#### E 1 A_ReFire("superstab4");
			goto fire;
		superstab4:
			CKF0 C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
			#### D 0 {
				A_MeleeWeaponAttack(HDCMW_ATTACK_THRUST, dmg: 6);
				hdplayerpawn(self).fatigue += 2;
			}
			#### D 5 offset(3, 31); // A_WeaponOffset(frandom(-12, 12), frandom(30, 34));
			#### CB 1;
			#### E 3;
			#### E 1 A_ReFire("superstab5");
			goto fire;
		superstab5:
			CKF0 C 1 A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);
			#### D 0 {
				A_MeleeWeaponAttack(HDCMW_ATTACK_THRUST, dmg: 25);
				hdplayerpawn(self).fatigue += 3;
			}
			#### D 9 offset(1, 32); // A_WeaponOffset(frandom(-7, 7), frandom(30, 34));
			#### CB 3;
			#### E 3;
			goto fire;
		zerksuperstab:
			CKF0 D 0 {A_Recoil(-1); A_WeaponReady(WRF_NOSWITCH|WRF_NOFIRE);}
			#### D 0 {
				A_MeleeWeaponAttack(HDCMW_ATTACK_THRUST, dmg: invoker.flicked ? 150 : 110);
				hdplayerpawn(self).fatigue += 4;
			}
			#### D 2 A_WeaponOffset(frandom(-12, 12), frandom(30, 34));
			#### CBE 1;
			#### E 1;
			// #### E 0 A_JumpIf(pressingAltFire(), "altfire");
			#### E 1 A_ReFire("zerksuperstab");
			goto ready;
		
		death:
			KNFP X 1 {
			invoker.bJUSTCHUCKED = false;
			A_SpawnItemEx("BulletPuffSmall");
			A_NoGravity();
				if (tracer) {
				
					let hdt = HDMobBase(tracer);
					
					if (
						pos.z - tracer.pos.z > tracer.height * 0.8
						&&(
							!hdt
							||(
								!hdt.bNOVITALSHOTS
								&&!hdt.bHEADLESS
							)
						)
					) {
						if (hd_debug) A_Log("HEAD SHOT");
						bPIERCEARMOR = true;
						damagemobj(tracer, self, 5, "slashing");
					}
					
					HDBleedingWound.inflict(tracer, 15);
				}
			}
			goto spawn3;
		Xdeath:
			KNFP X 1 {
				invoker.bJUSTCHUCKED = false;

				if (tracer) {
				
					let hdt = HDMobBase(tracer);
					
					if (
						pos.z - tracer.pos.z > tracer.height * 0.8
						&& (
							!hdt
							|| (
								!hdt.bNOVITALSHOTS
								&&!hdt.bHEADLESS
							)
						)
					) {
						if (hd_debug) A_Log("HEAD SHOT");
						bpiercearmor = true;
						damagemobj(tracer, self, 75, "slashing");
					}
					
					HDBleedingWound.inflict(tracer, 15);
				}
			}
			goto spawn3;
		Crash:
			KNFP X 1 {
				invoker.bJUSTCHUCKED = false;

				if (tracer) {
				
					let hdt = HDMobBase(tracer);
					
					if (
						pos.z - tracer.pos.z > tracer.height * 0.8
						&& (
							!hdt
							|| (
								!hdt.bNOVITALSHOTS
								&& !hdt.bHEADLESS
							)
						)
					) {
						if (hd_debug) A_Log("HEAD SHOT");
						bPIERCEARMOR = true;
						damagemobj(tracer, self, 75, "slashing");
					}

					HDBleedingWound.inflict(tracer, 35, 7);
					if(!tracer.bNOBLOOD) {
						A_StartSound("weapons/kharoncut", CHAN_7, volume: 0.6);
					}
				}
			}
			goto spawn3;
		unload:
			#### E 0;
			goto nope;
		ohnomyknife:
			TNT1 A 0;
			stop;
	}
}
